import edl;
extension util::EDLUtil;
extension util::JavaUtil;

/*
 * Validates a CAPI Model
 */

/*
 * CAPI Class Checks
 */

context EDLCAPIClass if (name != null)
	ERROR "[CAPI_CLASS_NAME_NOT_JAVA_ID] The class name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLCAPIClass if (name != null)
	WARNING "[CAPI_CLASS_NAME_RESERVED_JAVA_KEYWORD] The class name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLCAPIClass
	ERROR "[CAPI_CLASS_CYCLIC_INHERITANCE] The class '" + name + "' cannot inherit from itself.":
	!this.isSubClass(this);

context EDLCAPIClass if (!this.isSubClass(this))
	ERROR "[CAPI_CLASS_MEMBER_NAMES_NOT_UNIQUE] The member names are not unique.":
	this.getAllMembers().select(m | m.metaType != EDLCAPIOperation).forAll(
		m | this.getAllMembers().select(x | x.name == m.name && x.metaType != EDLCAPIOperation).size == 1);

context EDLCAPIClass
	WARNING "[CAPI_CLASS_MODIFIERS_NOT_UNIQUE] The modifiers of the class '" + name + "' are not unique.":
	modifiers.forAll(m | modifiers.select(x | x == m).size == 1);

/*
 * CAPI Interface Checks
 */

context EDLCAPIInterface if (name != null)
	ERROR "[CAPI_INTERFACE_NAME_NOT_JAVA_ID] The interface name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLCAPIInterface if (name != null)
	WARNING "[CAPI_INTERFACE_NAME_RESERVED_JAVA_KEYWORD] The interface name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLCAPIInterface
	ERROR "[CAPI_INTERFACE_CYCLIC_INHERITANCE] The interface '" + name + "' cannot inherit from itself.":
	!this.isImplementor(this);

/*
 * CAPI Attribute Checks
 */

context EDLCAPIAttribute if (name != null)
	WARNING "[CAPI_ATTRIBUTE_NAME_RESERVED_JAVA_KEYWORD] The attribute name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLCAPIAttribute if (name != null)
	ERROR "[CAPI_ATTRIBUTE_NAME_NOT_JAVA_ID] The attribute name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLCAPIAttribute
	WARNING "[CAPI_ATTRIBUTE_MODIFIERS_NOT_UNIQUE] The attribute modifiers are not unique.":
	modifiers.forAll(m | modifiers.select(x | x == m).size == 1);

/*
 * CAPI Constant Checks
 */

context EDLCAPIConstant if (name != null)
	WARNING "[CAPI_CONSTANT_NAME_RESERVED_JAVA_KEYWORD] The constant name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLCAPIConstant if (name != null)
	ERROR "[CAPI_CONSTANT_NAME_NOT_JAVA_ID] The constant name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLCAPIConstant if ((defaultValue != null) && (type.getClassName() == "boolean"))
	ERROR "[CAPI_CONSTANT_WRONG_BOOLEAN_DEFAULT] The boolean constant '" + name + "' can only have a boolean default value.":
	defaultValue.metaType == EDLBooleanLiteral;	

/*
 * CAPI Relation Checks
 */

context EDLCAPIRelation if (name != null)
	WARNING "[CAPI_RELATION_NAME_RESERVED_JAVA_KEYWORD] The relation name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLCAPIRelation if (name != null)
	ERROR "[CAPI_RELATION_NAME_NOT_JAVA_ID] The relation name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLCAPIRelation
	WARNING "[CAPI_RELATION_MODIFIERS_NOT_UNIQUE] The relation modifiers are not unique.":
	modifiers.forAll(m | modifiers.select(x | x == m).size == 1);

/*
 * CAPI Operation Checks
 */

context EDLCAPIOperation if (name != null)
	ERROR "[CAPI_OPERATION_NAME_RESERVED_JAVA_ID] The operation name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLCAPIOperation if (name != null)
	ERROR "[CAPI_OPERATION_NAME_NOT_JAVA_ID] The operation name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLCAPIOperation
	ERROR "[CAPI_PARAMETER_NAMES_NOT_UNIQUE] The parameter names are not unique.":
	parameters.forAll(p | parameters.select(x | x.name == p.name).size == 1);

/*
 * CAPI Operation Parameter Checks
 */

context EDLCAPIOperationParameter if (name != null)
	WARNING "[CAPI_PARAMETER_NAME_RESERVED_JAVA_KEYWORD] The parameter name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLCAPIOperationParameter if (name != null)
	ERROR "[CAPI_PARAMETER_NAME_NOT_JAVA_ID] The parameter name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();
