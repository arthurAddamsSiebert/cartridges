import edl;
extension util::EDLUtil;
extension util::JavaUtil;

/*
 * Validates an XML Model.
 */

/*
 * XML Type Checks
 */

context EDLXMLType
	ERROR "[XML_CLASS_CYCLIC_INHERITANCE] The class '" + name + "' cannot inherit from itself.":
	!this.isSubClass(this);

context EDLXMLType if (name != null)
	ERROR "[XML_CLASS_NAME_NOT_JAVA_ID] The class name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLXMLType if (name != null)
	ERROR "[XML_CLASS_NAME_RESERVED_JAVA_KEYWORD] The class name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLXMLType
	WARNING "[XML_CLASS_MODIFIERS_NOT_UNIQUE] The class modifiers are not unique.":
	modifiers.forAll(m | modifiers.select(x | x == m).size == 1);

context EDLXMLType if (!this.isSubClass(this))
	ERROR "[XML_CLASS_MEMBER_NAMES_NOT_UNIQUE] The member names are not unique.":
	getAllMembers().forAll(m | getAllMembers().select(x | x.name == m.name).size == 1);

context EDLXMLType if (xmlName != null)
	ERROR "[XML_CLASS_XMLNAME_NOT_XML_ID] The XML name '" + xmlName + "' is not a valid XML identifier.":
	xmlName.isValidXMLIdentifier();

context EDLXMLType if (!this.isSubClass(this))
	ERROR "[XML_CLASS_MULTIPLE_ATTRIBUTES_AS_TEXT] The class '" + name + "' can have only one attribute with XML format 'text'.":
	getAllAttributes().select(a | a.xmlFormat == EDLXMLFormat::text).size <= 1;

context EDLXMLType if (!this.isSubClass(this))
	ERROR "[XML_CLASS_MULTIPLE_ATTRIBUTES_SAME_XML] The class '" + name + "' has multiple attributes with the same XML format and the same XML name.":
	getAllAttributes().forAll(a | getAllAttributes().select(x | (x.xmlFormat == a.xmlFormat) && 
		(x.getXMLName() == a.getXMLName())).size == 1);

context EDLXMLType if (!this.isSubClass(this))
	ERROR "[XML_CLASS_MULTIPLE_CONTAINMENTS_SAME_XML] The class '" + name + "' has multiple containments with the same XML format and the same XML name.":
	getAllContainments().forAll(c | getAllContainments().select(x | (x.xmlFormat == c.xmlFormat) && 
		(x.getXMLName() == c.getXMLName())).size == 1);

/*
 * XML Attribute Checks
 */

context EDLXMLAttribute if (name != null)
	ERROR "[XML_ATTRIBUTE_NAME_NOT_JAVA_ID] The attribute name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLXMLAttribute if (name != null)
	ERROR "[XML_ATTRIBUTE_NAME_RESERVED_JAVA_KEYWORD] The attribute name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLXMLAttribute if (xmlName != null)
	ERROR "[XML_ATTRIBUTE_XMLNAME_NOT_XML_ID] The XML name '" + xmlName + "' is not a valid XML identifier.":
	xmlName.isValidXMLIdentifier();

context EDLXMLAttribute if ((defaultValue != null) && (type.getClassName() == "boolean"))
	ERROR "[XML_ATTRIBUTE_WRONG_BOOLEAN_DEFAULT] The boolean attribute '" + name + "' can only have a boolean default value.":
	defaultValue.metaType == EDLBooleanLiteral;	

context EDLXMLAttribute if ((EDLXMLType) eContainer).isSubClass("com.intershop.beehive.core.common.DValue")
	ERROR "[XML_ATTRIBUTE_ARRAYS_NOT_IN_DVALUE] Arrays are not supported for DValues.":
	dimension.size == 0;

context EDLXMLAttribute if ((EDLXMLType) eContainer).isSubClass("com.intershop.beehive.core.common.DValue")
	ERROR "[XML_ATTRIBUTE_TYPE_NOT_IN_DVALUE] The attribute type is not supported for DValues.":
	type.isValidDValueAttributeType();

context EDLXMLAttribute if (((EDLXMLType) eContainer).isSubClass("com.intershop.beehive.core.common.DObject") &&
	dimension.size > 0)
	ERROR "[XML_ATTRIBUTE_XMLFORMAT_NOT_ELEMENT] Array attributes must have the XML format 'element'.":
	xmlFormat == EDLXMLFormat::element;

context EDLXMLAttribute if (((EDLXMLType) eContainer).isSubClass("com.intershop.beehive.core.common.DObject") &&
	xmlFormat == EDLXMLFormat::element)
	ERROR "[XML_ATTRIBUTE_TYPE_NOT_FOR_ELEMENT] The attribute type is not supported for the XML format 'element'.":
	type.isValidDObjectAttributeTypeElement();
	
context EDLXMLAttribute if (((EDLXMLType) eContainer).isSubClass("com.intershop.beehive.core.common.DObject") &&
	xmlFormat == EDLXMLFormat::attribute)
	ERROR "[XML_ATTRIBUTE_TYPE_NOT_FOR_ATTRIBUTE] The attribute type is not supported for the XML format 'attribute'.":
	type.isValidDObjectAttributeTypeAttribute();

context EDLXMLAttribute if (((EDLXMLType) eContainer).isSubClass("com.intershop.beehive.core.common.DObject") &&
	xmlFormat == EDLXMLFormat::text)
	ERROR "[XML_ATTRIBUTE_TYPE_NOT_FOR_TEXT] The attribute type is not supported for the XML format 'text'.":
	type.isValidDObjectAttributeTypeText();

/*
 * XML Containment Checks
 */

context EDLXMLContainment if (name != null)
	ERROR "[XML_CONTAINMENT_NAME_NOT_JAVA_ID] The containment name '" + name + "' is not a valid Java identifier.":
	name.unescape().isValidJavaIdentifier();

context EDLXMLContainment if (name != null)
	ERROR "[XML_CONTAINMENT_NAME_RESERVED_JAVA_KEYWORD] The containment name '" + name + "' is a reserved Java keyword.":
	!name.unescape().isJavaKeyword();

context EDLXMLContainment if (xmlName != null)
	ERROR "[XML_CONTAINMENT_XMLNAME_NOT_XML_ID] The XML name '" + xmlName + "' is not a valid XML identifier.":
	xmlName.isValidXMLIdentifier();
