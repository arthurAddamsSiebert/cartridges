import "enfinity:/app/edl/com/intershop/beehive/app/capi/App.edl";
import "enfinity:/core/edl/com/intershop/beehive/core/types.edl";
import "enfinity:/core/edl/com/intershop/beehive/core/capi/domain/PersistentObject.edl";
import "enfinity:/bc_pmc/edl/com/intershop/component/pmc/capi/pagelet/ConfigurationParameterCtnr.edl";
import "enfinity:/bc_pmc/edl/com/intershop/component/pmc/capi/pagelet/PageletEntryPoint.edl";

external Describable type "com.intershop.beehive.core.capi.util.Describable";
external Nameable type "com.intershop.beehive.core.capi.util.Nameable";
external ContextObjectRelation type "com.intershop.component.pmc.capi.definition.pagelet.ContextObjectRelation";
external PepCollection type "java.util.Collection<com.intershop.component.pmc.capi.pagelet.PageletEntryPoint>";
external PepVcAssignmentCollection type "java.util.Collection<com.intershop.component.pmc.capi.viewcontext.assignment.PageletEntryPointViewContextAssignment>";
external ViewContextReference type "com.intershop.component.pmc.capi.viewcontext.ViewContextReference";

namespace com.intershop.component.pmc.capi.viewcontext
{
    cartridge interface ViewContext extends ConfigurationParameterCtnr, Nameable, Describable
    {
        /*
         * The unique internal id.
         */
        attribute id: string required readonly;
        
        /*
         * The display name of the view context.
         */
        attribute displayName: string localized;

        /*
         * The description name of the view context.
         */
        attribute description: string localized;
        
        /*
         * The page flag indicates whether the view contexts ContextObjectRelation points to a page or an include.
         */
        attribute page : boolean readonly;
        
        /*
         * The system managed flag indicates whether the view contexts are instantiated by the system or created by the user.
         */
        attribute systemManaged : boolean readonly;
        
        /*
         * Returns the associated ContextObjectRelation of this view context instance.
         */
        operation getContextObjectRelation(): ContextObjectRelation;
        
        /*
         * Answers a collection containing all pagelet entry point fall-backs of this view context.
         */
        operation getPageletEntryPointFallbacks() : PepCollection;
        
        /*
         * Answers the pagelet entry point fall-back bound to this view context as defined by the content entry point definition inclusion of the
         * context object relation this view context is derived from and which is identified by the given qualified name.
         */
        operation getPageletEntryPointFallback(qualifiedName : String) : PageletEntryPoint;

        /*
         * Answers a collection containing all assignments connecting this view context with its pagelet entry point fall-backs. 
         */
        operation getPageletEntryPointViewContextAssignmentFallbacks() : PepVcAssignmentCollection;
        
        /*
         * Answers a collection of all pagelet entry points bound to this view context and objects according to the target object type 
         * of the context object relation this view context is derived from.
         */
        operation getPageletEntryPoints() : PepCollection;
        
        /*
         * Answers a collection of all pagelet entry points bound to the given object in the scope of this view context.
         * There may be not more pagelet entry points as the number of content entry point definition inclusions of the context object relation
         * this view context is derived from.
         */
        operation getPageletEntryPoints(object : PersistentObject) : PepCollection;
        
        /*
         * Returns a unique reference to the view context. 
         * The returned reference is unique with respect to the domain.
         */
        operation getViewContextReference() : ViewContextReference;
    }
}
