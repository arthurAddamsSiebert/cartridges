import "enfinity:/bts/edl/com/intershop/beehive/bts/capi/orderprocess/ProductLineItem.edl";
import "enfinity:/core/edl/com/intershop/beehive/core/capi/domain/ExtensibleObject.edl";
import "enfinity:/core/edl/com/intershop/beehive/core/types.edl";

external User type "com.intershop.beehive.core.capi.user.User";

namespace com.intershop.component.requisition.capi.delivery
{
    /*
     * A delivery note is used to track the delivery details purchase orders at the line item level. In case of partial deliveries, a single line item can have multiple delivery notes attached to it.
     * <p>
     * Delivery notes play a central role in the 'receive goods' process. The notes are used to connect physical deliveries (i.e. supplier provided packing slips) back to the purchase orders stored in the system. Since delivery notes work on the line item level, they support N:M relations between packing slip documents and purchase order documents.
     * 
     * @author Jan Fiedler
     */
    cartridge interface DeliveryNote extends ExtensibleObject
    {
        /*
         * The delivery note quantity serves a the primary tracking tool for goods deliveries. This attribute is set by the user receiving goods to represent the quantity of items he actually received.
         * <p>
         * In case the delivery quantity is smaller that the quantity in of the original line item, we have a partial delivery. The delivery quantities of all delivery note instances attached to a single line item are supposed to sum up to the purchase order line item quantity before the line item is considered to be delivered in full.
         */
        attribute quantity : Quantity required;

        /*
         * Comments can be used to track delivery related information. This information is maintained by the user that actually receives goods.
         */
        attribute comment : string required;

        /*
         * The document number references the actual paper delivery note or packing slip attached to the physical shipment.
         */
        attribute documentNo : string required;

        /*
         * The delivery timestamp captures the time of the delivery action. The timestamp will be set automatically to the creation time of the delivery note.
         */
        attribute ^timestamp : datetime required;

        /*
         * This relation references the line item the delivery note was created for. Note that multiple delivery notes are attached to a single line item in case of partial shipments.
         */
        relation lineItem : ProductLineItem[0..1] readonly;

        /*
         * The receiver represents the user that received the physical goods and who created the delivery note. This can be a sort of central user (in case of a central department taking care of incoming goods) or any other user that takes responsiblility (e.g. an assistant or the actual buyer that initiated the requisition in the first place).
         */
        relation representative : User[0..1];
    }
}
